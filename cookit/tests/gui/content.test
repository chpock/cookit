# cookit tests - content
#
# Copyright (C) 2024 Konstantin Kushnir <chpock@gmail.com>
#
# See the file "license.terms" for information on usage and redistribution of
# this file, and for a DISCLAIMER OF ALL WARRANTIES.

# These tests check the expected content in cookit vfs

if { [lsearch [namespace children] ::tcltest] == -1 } {
    package require tcltest 2
    namespace import -force ::tcltest::*
}

package require Tk
testConstraint pkgconfig [llength [info commands ::tk::pkgconfig]]

test content-2.1.1 {check Tcl version} -body {
    expr { [package require Tcl] in {8.6.15 9.0.0} }
} -result 1

test content-2.1.2 {check Tk version} -body {
    expr { [package require Tk] in {8.6.15 9.0.0} }
} -result 1

test content-2.1.3 {check Ttk version} -body {
    expr { [package require Ttk] in {8.6.15 9.0.0} }
} -result 1

test content-2.2.1 {check Thread version} -body {
    expr { [package require Thread] in {2.8.10 3.0.0} }
} -result 1

foreach { pkgName pkgVersion constraints } {
    http                2.9.8  {}
    https               0.1.0  {}
    msgcat              1.6.1  {}
    registry            1.3.5  {win}
    twapi               5.0b1  {win}
    vfs                 1.4.2  {}
    cookfs              1.9.0  {}
    tdom                0.9.5  {}
} {
    test content-3.[incr count] [list package require $pkgName $pkgVersion] \
        -constraints $constraints \
        -body [list package require $pkgName $pkgVersion] \
        -returnCodes ok -match glob -result *
}
unset count

# For some unknown reason, tk::pkgconfig in Tk9 on Windows has no paths.
test content-8 {::tk::pkgconfig values} -constraints { pkgconfig unix } -body {
    set result [list]
    foreach { key1 key2 val } [list \
        libdir,runtime     libdir,install      [file join $::cookit::root lib] \
        bindir,runtime     bindir,install      [file join $::cookit::root bin] \
        scriptdir,runtime  scriptdir,install   [file join $::cookit::root lib tk[info tclversion]] \
        includedir,runtime includedir,install  [file join $::cookit::root include] \
        docdir,runtime     docdir,install      [file join $::cookit::root share man] \
        demodir,runtime    demodir,install     [file join $::cookit::root lib tk[info tclversion] demos] \
    ] {
        if { [::tk::pkgconfig get $key1] ne $val } {
            lappend result "::tk::pkgconfig get $key1 != '$val' (actual value: '[::tk::pkgconfig get $key1]')"
        }
        if { [::tk::pkgconfig get $key2] ne $val } {
            lappend result "::tk::pkgconfig get $key2 != '$val' (actual value: '[::tk::pkgconfig get $key2]')"
        }
    }
    join $result \n
} -result {}

# cleanup
::tcltest::cleanupTests
return
